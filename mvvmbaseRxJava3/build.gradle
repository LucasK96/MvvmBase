apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// for Bintray
def projectVersionCodeNr = Integer.parseInt(projectVersionCode)
def libGit = libGit
def libUrl = libUrl
def libDescription = libRxJava3Description
def libGroupId = libGroupId
def libArtifactId = libRxJava3ArtifactId

// for Maven
group = libGroupId
def projectVersionName = projectVersionName

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode = projectVersionCodeNr
        versionName = projectVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8" // required by safeargs plugin for "by navArgs()"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation project(":mvvmbase")

    implementation "io.reactivex.rxjava3:rxkotlin:3.0.0"

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}


apply plugin: 'com.github.dcendents.android-maven'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            groupId libGroupId
            artifactId libArtifactId

            // Add your description here
            name libArtifactId
            description libDescription
            url libUrl

            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url libLicenseUrl
                }
            }
            scm {
                connection libGit
                developerConnection libGit
                url libUrl
            }
        }

    }
}

apply plugin: 'com.jfrog.bintray'

version = projectVersionName

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

artifacts {
    archives sourcesJar
}

// Bintray
bintray {
    user = private_bintray_user
    key = private_bintray_apikey

    configurations = ['archives']
    pkg {
        repo = libGroupId
        name = libArtifactId
        desc = libDescription
        websiteUrl = libUrl
        vcsUrl = libGit
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            name = projectVersionName
            desc = libDescription
            gpg {
                sign = true
                passphrase = private_bintray_gpg_passphrase
            }
        }
    }
}
